
import "decode"


func extract_op(instr: Nat32) -> Nat8 {
    return Nat8 (instr and 0x7F)
}


func extract_funct3(instr: Nat32) -> Nat8 {
    return Nat8 ((instr >> 12) and 0x07)
}


func extract_rd(instr: Nat32) -> Nat8 {
    return Nat8 ((instr >> 7) and 0x1F)
}


func extract_rs1(instr: Nat32) -> Nat8 {
    return Nat8 ((instr >> 15) and 0x1F)
}


func extract_rs2(instr: Nat32) -> Nat8 {
    return Nat8 ((instr >> 20) and 0x1F)
}


func extract_funct7(instr: Nat32) -> Nat8 {
    return Nat8 ((instr >> 25) and 0x7F)
}


// bits: (31 .. 20)
func extract_imm12(instr: Nat32) -> Nat32 {
    return (instr >> 20) and 0xFFF
}


func extract_imm31_12(instr: Nat32) -> Nat32 {
    return (instr >> 12) and 0xFFFFF
}


func extract_jal_imm(instr: Nat32) -> Nat32 {
    let imm = extract_imm31_12(instr)
    let bit19to12_msk = ((imm >> 0) and 0xFF) << 12
    let bit11_msk = ((imm >> 8) and 0x1) << 11
    let bit10to1 = ((imm >> 9) and 0x3FF) << 1
    let bit20_msk = ((imm >> 20) and 0x1) << 20
    return bit20_msk or bit19to12_msk or bit11_msk or bit10to1
}



// sign expand (12bit -> 32bit)
func expand12(val_12bit: Nat32) -> Int32 {
    var v = val_12bit
    if (v and 0x800) != 0 {
        v = v or 0xFFFFF000
    }
    return Int32 v
}

// sign expand (20bit -> 32bit)
func expand20(val_20bit: Nat32) -> Int32 {
    var v = val_20bit
    if (v and 0x80000) != 0 {
        v = v or 0xFFF00000
    }
    return Int32 v
}

